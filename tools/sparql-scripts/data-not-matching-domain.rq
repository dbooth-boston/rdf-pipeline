############ Check for data not matching declared domain:
prefix rdfs:    <http://www.w3.org/2000/01/rdf-schema#>
prefix schemaorg:       <https://schema.org/>
prefix owl:     <http://www.w3.org/2002/07/owl#>
prefix rdf:     <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
select DISTINCT ?property ?domain ?subject {
  ?subject ?property ?v .
  FILTER ( !strStarts(str(?property), str(owl:)) )
  FILTER ( !strStarts(str(?property), str(rdf:)) )
  FILTER ( !strStarts(str(?property), str(rdfs:)) )
  FILTER ( !strStarts(str(?property), str(schemaorg:)) )
  ?subject a ?domain .
  FILTER ( !(?domain = rdfs:Resource || ?domain = owl:Thing) )
  FILTER ( !(isLiteral($subject) && datatype($subject) = ?domain) )
  FILTER NOT EXISTS {
          { ?property rdfs:domain ?domain . }
            UNION { ?property schemaorg:domainIncludes ?domain . } }
}
ORDER BY ?property ?domain ?subject

